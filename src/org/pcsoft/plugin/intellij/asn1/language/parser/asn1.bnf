{
    parserClass="org.pcsoft.plugin.intellij.asn1.language.parser.Asn1Parser"
    parserUtilClass="org.pcsoft.plugin.intellij.asn1.language.parser.Asn1ParserUtil"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Asn1"
    psiImplClassSuffix="Impl"
    psiPackage="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element"
    psiImplPackage="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element"

    elementTypeHolderClass="org.pcsoft.plugin.intellij.asn1.language.parser.token.Asn1GenElementFactory"
    elementTypeClass="org.pcsoft.plugin.intellij.asn1.language.parser.token.Asn1ElementType"
    tokenTypeClass="org.pcsoft.plugin.intellij.asn1.language.parser.token.Asn1TokenType"

    psiImplUtilClass="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.util.Asn1ElementUtils"
}

asn1File ::= moduleDefinition

/**********************************************************************************************************************/
/******************************* Module Definition ********************************************************************/
/**********************************************************************************************************************/

moduleDefinition ::= moduleIdentifier 'DEFINITIONS' encodingReferenceDefault? tagDefault? extensionDefault? '::=' moduleBody encodingControlDefinition? {
                     mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
                     implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
                     methods=[getName setName getNameIdentifier getPresentation]
}
moduleIdentifier ::= moduleReferenceTokenSet (definitiveObjectIdentifier definitiveString?)?
{
    methods=[getName]
}
private tagDefault ::= ['IMPLICIT' | 'EXPLICIT' | 'AUTOMATIC'] 'TAGS'
private encodingReferenceDefault ::= encodingReferenceTokenSet 'INSTRUCTIONS'
private extensionDefault ::= 'EXTENSIBILITY IMPLIED'
private moduleBody ::= 'BEGIN' exportDefinition? importDefinition? moduleContent 'END'
moduleContent ::= (objectClassDefinition | enumeratedDefinition | typeDefinition | objectValueDefinition | objectSetDefinition)*
/**********************************************************************************************************************/
/********************************* Module Imports / Exports ***********************************************************/
/**********************************************************************************************************************/
importDefinition ::= 'IMPORTS' importBody
private importBody ::= importContent? ';'
importContent ::= importElement+
importElement ::= importExportSymbols 'FROM' moduleRef

exportDefinition ::= 'EXPORTS' exportBody
private exportBody ::= ('ALL' | exportContent)? ';'
exportContent ::= importExportSymbols

private importExportSymbols ::= (importExportSymbol ',')* importExportSymbol
importExportSymbol ::= importExportSymbolRef ('{' '}')?

/**********************************************************************************************************************/
/********************************* Encoding Control Definition ********************************************************/
/**********************************************************************************************************************/

encodingControlDefinition ::= encodingControlElement+
encodingControlElement ::= 'ENCODING-CONTROL' encodingReferenceTokenSet

/**********************************************************************************************************************/
/******************************* Type Definition **********************************************************************/
/**********************************************************************************************************************/

typeDefinition ::= typeIdentifier parameterDefinition? '::='  tagDefinition? (nativeType | elementDefinitionType)  constraintDefinition? typeDefinitionBody? constraintDefinition? modifiers
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
typeIdentifier ::= typeReferenceTokenSet
{
    methods=[getName]
}
private typeDefinitionBody ::= '{' (typeDefinitionContent | constantDefinitionContent)? '}'
typeDefinitionContent ::= (typeDefinitionField ',')* (typeDefinitionField | futureExtension)
/**********************************************************************************************************************/
/********************** Type Definition Field *************************************************************************/
/**********************************************************************************************************************/
typeDefinitionField ::= typeFieldIdentifier tagDefinition? (nativeType | objectClassDefinitionFieldType | elementDefinitionType) constraintDefinition? typeDefinitionFieldBody? constraintDefinition? modifiers
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
typeFieldIdentifier ::= typeFieldReferenceTokenSet
{
    methods=[getName]
}
private typeDefinitionFieldBody ::= '{' (typeDefinitionContent | constantDefinitionContent) '}'

/**********************************************************************************************************************/
/******************** Object Class Definition *************************************************************************/
/**********************************************************************************************************************/

objectClassDefinition ::= objectClassDefinitionName '::=' 'CLASS' objectClassDefinitionBody objectClassDefinitionConstructor?
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
objectClassDefinitionName ::= objectClassDefinitionNameTokenSet
{
    methods=[getName]
}
private objectClassDefinitionBody ::= '{' objectClassDefinitionContent? '}'
objectClassDefinitionContent ::= (objectClassDefinitionField ',')* (objectClassDefinitionField | futureExtension)
/**********************************************************************************************************************/
/**************** Constructor Definition ******************************************************************************/
/**********************************************************************************************************************/
objectClassDefinitionConstructor ::= 'WITH SYNTAX' '{' objectClassDefinitionConstructorContent '}'
objectClassDefinitionConstructorContent ::= ( ('&' objectClassDefinitionFieldRef) | objectClassConstructorTokenSet | ('[' objectClassDefinitionConstructorContent ']')) +

objectClassDefinitionConstructorCall ::= '{' objectClassDefinitionConstructorCallContent '}'
objectClassDefinitionConstructorCallContent ::= (objectClassDefinitionConstructorCallContentType | objectClassDefinitionConstructorCallContentValue)+
private objectClassDefinitionConstructorCallContentType ::= (anyType | (nativeType objectClassDefinitionConstructorCallContentTypeContent?) | objectClassDefinitionConstructorCallArgumentRef) constraintDefinition?
private objectClassDefinitionConstructorCallContentValue ::= smallDefineDefinition
objectClassDefinitionConstructorCallArgumentRef ::= typeReferenceTokenSet | objectValueDefinitionNameTokenSet | objectSetDefinitionNameTokenSet
private objectClassDefinitionConstructorCallContentTypeContent ::= '{' (typeDefinitionContent | constantDefinitionContent) '}'
/**********************************************************************************************************************/
/*************** Object Class Definition Field ************************************************************************/
/**********************************************************************************************************************/
objectClassDefinitionField ::= '&' objectClassDefinitionFieldName (nativeType | elementDefinitionType)? constraintDefinition? objectClassDefinitionFieldBody? modifiers
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
objectClassDefinitionFieldName ::= objectClassDefinitionFieldNameTokenSet
{
    methods=[getName]
}
private objectClassDefinitionFieldBody ::= '{' (typeDefinitionContent | constantDefinitionContent) '}'

/**********************************************************************************************************************/
/**************** Value Definition ************************************************************************************/
/**********************************************************************************************************************/

objectValueDefinition ::= objectValueDefinitionName (nativeType | fullQualifiedObjectClassDefinitionFieldRef | elementDefinitionType) '::=' fullDefineDefinition
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
objectValueDefinitionName ::= objectValueDefinitionNameTokenSet
{
    methods=[getName]
}

/**********************************************************************************************************************/
/**************** Object Set Definition *******************************************************************************/
/**********************************************************************************************************************/

objectSetDefinition ::= objectSetDefinitionName objectClassDefinitionRef '::=' objectSetDefinitionBody
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
objectSetDefinitionName ::= objectSetDefinitionNameTokenSet
{
    methods=[getName]
}
private objectSetDefinitionBody ::= '{' objectSetDefinitionContent? '}'
objectSetDefinitionContent ::= ((objectSetDefinitionContentList | "...") ',')* (objectSetDefinitionContentList | "...")
private objectSetDefinitionContentList ::= ((objectSetDefinitionContentElement | '...') '|')* (objectSetDefinitionContentElement | "...")
private objectSetDefinitionContentElement ::= objectClassDefinitionConstructorCall | fullQualifiedObjectClassDefinitionFieldRef | objectValueDefinitionRef

/**********************************************************************************************************************/
/**************** Enumerated Definition *******************************************************************************/
/**********************************************************************************************************************/

enumeratedDefinition ::= enumeratedDefinitionName '::=' 'ENUMERATED' enumeratedDefinitionBody
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
enumeratedDefinitionName ::= enumeratedDefinitionNameTokenSet
{
    methods=[getName]
}
private enumeratedDefinitionBody ::= '{' enumeratedDefinitionContent? '}'
enumeratedDefinitionContent ::= (enumeratedDefinitionElement ',')* (enumeratedDefinitionElement | '...')
enumeratedDefinitionElement ::= enumeratedDefinitionElementTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}

/**********************************************************************************************************************/
/******************** Tag Definition **********************************************************************************/
/**********************************************************************************************************************/

tagDefinition ::= '[' 'APPLICATION'? NUMBER ']' ('IMPLICIT' | 'EXPLICIT')?
{
    methods=[getTagNumber getTaggingType getTagType]
}

/**********************************************************************************************************************/
/**************** Define Definition ***********************************************************************************/
/**********************************************************************************************************************/

private smallDefineDefinition ::= definitiveBoolean | NUMBER | definitiveString | valueRef | definitiveObjectIdentifier
private fullDefineDefinition ::= smallDefineDefinition | definitiveType | objectClassDefinitionConstructorCall
definitiveObjectIdentifier ::= '{' definitiveObjectIdentifierPart+ '}'
definitiveObjectIdentifierPart ::= NUMBER | constantDefinitionValue
{
    methods=[getName getNumber]
}
definitiveString ::= '"' TEXT '"'
{
    methods=[getStringValue]
}
definitiveType ::= '{' (definitiveTypeField ',')* definitiveTypeField? '}'
definitiveTypeField ::= typeDefinitionFieldRef smallDefineDefinition
private definitiveBoolean ::= 'TRUE' | 'FALSE'

/**********************************************************************************************************************/
/**************** Constraint Definition *******************************************************************************/
/**********************************************************************************************************************/

constraintDefinition ::= ('(' (constraintInBraces '|')* (constraintInBraces (',' '...')?) ')') | (constraintContentWithKeyword (',' '...')?)
private constraintInBraces ::= constraintContentWithValue | constraintContentWithKeyword | constraintDefinition
private constraintContentWithKeyword ::= constraintValueSize | constraintValueFrom | constraintValueComponent
private constraintContentWithValue ::= constraintValueDirect

constraintValueSize ::= 'SIZE' '(' constraintValueSizeContent ')'
private constraintValueSizeValueSet ::= NUMBER | 'MIN' | 'MAX' | valueRef
private constraintValueSizeContent ::= constraintValueSizeValueSet ('..' constraintValueSizeValueSet)? ('|' constraintValueSizeContent)?

constraintValueFrom ::= 'FROM' '(' constraintValueFromContent ')'
private constraintValueFromValueSet ::= definitiveString | valueRef
private constraintValueFromContent ::= constraintValueFromValueSet ('..' constraintValueFromValueSet)? ('|' constraintValueFromContent)?

constraintValueDirect ::= constraintValueDirectContent
private constraintValueDirectValueSet ::= valueRef | NUMBER | 'MIN' | 'MAX'
private constraintValueDirectContent ::= constraintValueDirectValueSet ('..' constraintValueDirectValueSet)?

constraintValueComponent ::= 'WITH COMPONENTS' '{' constraintValueComponentContent '}'
private constraintValueComponentContent ::= (constraintValueComponentContentField ',')* constraintValueComponentContentField
private constraintValueComponentContentField ::= '...' | (typeDefinitionFieldRef constraintValueComponentContentModifier)
private constraintValueComponentContentModifier ::= 'PRESENT' | 'ABSENT'


/**********************************************************************************************************************/
/**************** Constant Definition *********************************************************************************/
/**********************************************************************************************************************/

constantDefinitionContent ::= (constantDefinitionValue ',')* constantDefinitionValue
constantDefinitionValue ::= (constantDefinitionValueName '(' smallDefineDefinition ')') | valueRef
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
constantDefinitionValueName ::= constantDefinitionValueNameTokenSet
{
    methods=[getName]
}

/**********************************************************************************************************************/
/**************** Type Definition *************************************************************************************/
/**********************************************************************************************************************/

nativeType ::= primitiveType | listType | anyType | instanceOfType
private primitiveType ::= PRIMITIVE_TYPE
private listType ::= LIST_TYPE (constraintDefinition? 'OF' (nativeType | objectClassDefinitionFieldType | elementDefinitionType))?
private anyType ::= 'ANY' ('DEFINED BY' typeDefinitionFieldRef)?
private instanceOfType ::= 'INSTANCE OF' (primitiveType | listType | anyType | elementDefinitionType)

elementDefinitionType ::= elementDefinitionRef ('{' (elementDefinitionArgument ',')* elementDefinitionArgument '}')?
private elementDefinitionArgument ::= '{' (elementDefinitionObjectSetArgumentRef | '...') '}' | elementDefinitionTypeArgumentRef | '...'
elementDefinitionObjectSetArgumentRef ::= parameterNameTokenSet | objectSetDefinitionNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReferences getObjectSetParameterReference getObjectSetDefinitionReference]
}
elementDefinitionTypeArgumentRef ::= parameterNameTokenSet | objectClassDefinitionNameTokenSet | typeReferenceTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReferences getTypeParameterReference getObjectClassDefinitionReference getTypeDefinitionReference]
}

objectClassDefinitionFieldType ::= fullQualifiedObjectClassDefinitionFieldRef objectClassDefinitionRefAttribute?
private objectClassDefinitionRefAttribute ::= '(' '{' elementDefinitionObjectSetArgumentRef '}' ('{' '@' typeDefinitionFieldRef '}')? ')'

/**********************************************************************************************************************/
/**************** Parameter Definition ********************************************************************************/
/**********************************************************************************************************************/

parameterDefinition ::= '{' (parameter ',')* parameter '}'
private parameter ::= objectSetParameter | typeParameter

parameterName ::= parameterNameTokenSet
{
    methods=[getName]
}

objectSetParameter ::= objectSetParameterTypeRef ':' parameterName
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}
objectSetParameterTypeRef ::= objectClassDefinitionNameTokenSet | parameterNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReferences getTypeDefinitionReference getObjectClassDefinitionReference getTypeParameterReference]
}
typeParameter ::= parameterName
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getPresentation]
}

/**********************************************************************************************************************/
/******************* Reference Definition *****************************************************************************/
/**********************************************************************************************************************/
typeDefinitionRef ::= typeReferenceTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}
typeDefinitionFieldRef ::= typeFieldReferenceTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}
objectClassDefinitionRef ::= objectClassDefinitionNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}
parameterRef ::= parameterNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}
objectClassDefinitionFieldRef ::= objectClassDefinitionFieldNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}
fullQualifiedObjectClassDefinitionFieldRef ::= fullQualifiedObjectClassDefinitionFieldQualifierRef '.' '&' objectClassDefinitionFieldRef
fullQualifiedObjectClassDefinitionFieldQualifierRef ::= objectClassDefinitionNameTokenSet | objectValueDefinitionNameTokenSet
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReferences getObjectClassDefinitionReference getObjectValueDefinitionReference]
}
objectValueDefinitionRef ::= objectValueDefinitionNameTokenSet
constantDefinitionValueRef ::= constantDefinitionValueNameTokenSet
valueRef ::= constantDefinitionValueNameTokenSet | objectValueDefinitionNameTokenSet
{
     mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
     implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
     methods=[getName setName getNameIdentifier getReferences getConstantReference getObjectValueDefinitionReference getEnumeratedDefinitionElementReference]
}
importExportSymbolRef ::=  objectClassDefinitionNameTokenSet | typeReferenceTokenSet | objectValueDefinitionNameTokenSet | objectSetDefinitionNameTokenSet
{
     mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
     implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
     methods=[getName setName getNameIdentifier getReferences getTypeDefinitionReference getObjectClassDefinitionReference getObjectValueDefinitionReference getObjectSetDefinitionReference getEnumeratedDefinitionReference]
}
elementDefinitionRef ::= objectClassDefinitionNameTokenSet | typeReferenceTokenSet | enumeratedDefinitionNameTokenSet
{
     mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
     implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
     methods=[getName setName getNameIdentifier getReferences getObjectClassDefinitionReference getTypeDefinitionReference getEnumeratedReference]
}
moduleRef ::= importElementModuleTokenSet <<parseModuleRefOID definitiveObjectIdentifier>>
{
    mixin="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElementImpl"
    implements="org.pcsoft.plugin.intellij.asn1.language.parser.psi.element.Asn1NamedElement"
    methods=[getName setName getNameIdentifier getReference]
}

/**********************************************************************************************************************/
/******************* Others *******************************************************************************************/
/**********************************************************************************************************************/

private modifiers ::= ('OPTIONAL' | 'UNIQUE' | ('DEFAULT' smallDefineDefinition))*
private futureExtension ::= '...'

/**********************************************************************************************************************/
/******************* Basics *******************************************************************************************/
/**********************************************************************************************************************/

private encodingReferenceTokenSet ::= NAME_UPPER
private moduleReferenceTokenSet ::= NAME_CAP | NAME_UPPER
private importElementModuleTokenSet ::= NAME_CAP | NAME_UPPER
private typeReferenceTokenSet ::= NAME_CAP | NAME_UPPER
private typeFieldReferenceTokenSet ::= NAME_NO_CAP | NAME_LOWER
private objectClassDefinitionNameTokenSet ::= NAME_UPPER
private objectClassDefinitionFieldNameTokenSet ::= NAME_NO_CAP | NAME_CAP | NAME_LOWER
private objectValueDefinitionNameTokenSet ::= NAME_NO_CAP | NAME_LOWER
private objectClassConstructorTokenSet ::= NAME_UPPER
private objectSetDefinitionNameTokenSet ::= NAME_CAP | NAME_UPPER
private enumeratedDefinitionNameTokenSet ::= NAME_CAP | NAME_UPPER
private enumeratedDefinitionElementTokenSet ::= NAME_NO_CAP | NAME_LOWER
private constantDefinitionValueNameTokenSet ::= NAME_LOWER | NAME_NO_CAP
private parameterNameTokenSet ::= NAME_CAP | NAME_UPPER
